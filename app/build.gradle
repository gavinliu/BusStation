apply plugin: 'com.android.application'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
def gitCommitCount = 100 + Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())


def umeng_appkey = properties.getProperty("UMENG_APPKEY")

def release_storePassword = properties.getProperty("storePassword")
def release_keyAlias = properties.getProperty("keyAlias")
def release_keyPassword = properties.getProperty("keyPassword")


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "cn.gavinliu.bus.station"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode gitCommitCount
        versionName gitTag

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""

        return void
    }

    signingConfigs {
        release {
            storeFile file("app-release.jks")
            storePassword release_storePassword
            keyAlias release_keyAlias
            keyPassword release_keyPassword
        }

        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    productFlavors {
        meizu {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "MEIZU"]
        }
    }

    buildTypes {
        all {
            zipAlignEnabled true
            manifestPlaceholders = [UMENG_APPKEY_VALUE: umeng_appkey]
        }

        release {
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }

    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/rxjava.PROPERTIES'
        exclude 'META-INF/RXJAVA.properties'
        exclude 'META-INF/RXJAVA.PROPERTIES'
        exclude 'META-INF/rxjava'
        exclude 'META-INF/RXJAVA'
    }

    return void
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:preference-v7:25.2.0'
    compile 'com.android.support:recyclerview-v7:25.2.0'

    compile 'io.reactivex:rxandroid:1.2.1'

    compile 'com.squareup.okhttp3:logging-interceptor:3.3.1'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:converter-scalars:2.1.0'

    compile 'com.squareup:otto:1.3.8'

    compile 'com.umeng.analytics:analytics:latest.integration'
}
